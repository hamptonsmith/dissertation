

\section{Realizations\label{apx:real}}
%----------------------------------------------------------------

%-----------------------------------------------------------------------------
	\subsection{Integer\_Template}	%-----------------------------------------------------------------------------

%-----------------------------------------------------------------------------
		\subsubsection{Iterative\_Add\_to\_Realiz}	%-----------------------------------------------------------------------------

\begin{lstlisting}[language=resolve]
Realization Iterative_Add_to_Realiz for Adding_Capability of Integer_Template;
		uses Integer_Theory, Boolean_Theory, Std_Integer_Fac, Std_Boolean_Fac;

	Procedure Add_to(updates i : Integer; evaluates j : Integer);
		While (Not(Is_Zero(j))) 
			changing i, j;
			maintaining (i + j = #i + #j) and j >= 0;
			decreasing j;
		do
			Increment(i);
			Decrement(j);
		end;
	end Add_to;
end;
\end{lstlisting}

%-----------------------------------------------------------------------------
		\subsubsection{Iterative\_Multiply\_into\_Realiz}	%-----------------------------------------------------------------------------

\begin{lstlisting}[language=resolve]
Realization Iterative_Multiply_into_Realiz for Multiplying_Capability of Integer_Template;
		uses Integer_Theory, Boolean_Theory;

	Procedure Multiply_into(updates i : Integer; evaluates j : Integer);
		Var result : Integer;

		While (j /= 0) 
			changing result, j;
			maintaining result = i * (#j - j) and j >= 0;
			decreasing j;
		do
			result := result + i;
			j := j - 1;
		end;

		i :=: result;
	end;
end;
\end{lstlisting}

%-----------------------------------------------------------------------------
		\subsubsection{Recursive\_Add\_to\_Realiz}	%-----------------------------------------------------------------------------

\begin{lstlisting}[language=resolve]
Realization Recursive_Add_to_Realiz for Adding_Capability of Integer_Template;
	uses Std_Boolean_Fac;

	Recursive Procedure Add_to(updates i:Integer; evaluates j:Integer);
		decreasing j;

		If (not Is_Zero(j)) then
			Increment (i);
			Decrement (j);
			Add_to (i, j);
		end;
	end Add_to;
end;
\end{lstlisting}

%-----------------------------------------------------------------------------
	\subsection{Queue\_Template}	%-----------------------------------------------------------------------------

%-----------------------------------------------------------------------------
		\subsubsection{Recursive\_Flipping\_Realiz}	%-----------------------------------------------------------------------------

\begin{lstlisting}[language=resolve]
Realization Recursive_Flipping_Realiz for Flipping_Capability of Queue_Template;
    Recursive Procedure Flip(updates Q: Queue);
        decreasing |Q|;

        Var E: Entry;
        If (Length(Q) /= 0) then
            Dequeue(E, Q);
            Flip(Q);
            Enqueue(E, Q);
        end;
    end Flip;
end;
\end{lstlisting}

%-----------------------------------------------------------------------------
		\subsubsection{Selection\_Sort\_Realiz}	%-----------------------------------------------------------------------------

\begin{lstlisting}[language=resolve]
Realization Selection_Sort_Realization(
			Operation Compare(restores E1, E2 : Entry) 
				: Boolean;
				ensures Compare = LEQV(E1, E2);)
		for Sorting_Capability of Queue_Template;
	uses String_Theory;

	Procedure Sort(updates Q : Queue);
		Var Sorted_Queue : Queue;
		Var Lowest_Remaining : Entry;

		While (Length(Q) > 0)
			changing Q, Sorted_Queue, Lowest_Remaining;
			maintaining Is_Permutation(Q o Sorted_Queue, #Q) and
				Is_Conformal_With(LEQV, Sorted_Queue) and 
				Is_Universally_Related(Sorted_Queue, Q, LEQV);
			decreasing |Q|;
		do
			Remove_Min(Q, Lowest_Remaining);
			Enqueue(Lowest_Remaining, Sorted_Queue);
		end;
		Q :=: Sorted_Queue;
	end Sort;

	Operation Remove_Min(updates Q : Queue; replaces Min : Entry);
		requires |Q| /= 0;
		ensures Is_Permutation(Q o <Min>, #Q) and
			Is_Universally_Related(<Min>, Q, LEQV) and
			|Q| = |#Q| - 1;
	Procedure
		Var Considered_Entry : Entry;
		Var New_Queue : Queue;
		Dequeue(Min, Q);		
		While (Length(Q) > 0)
			changing Q, New_Queue, Min, Considered_Entry;
			maintaining Is_Permutation(
					New_Queue o Q o <Min>, #Q) and
				Is_Universally_Related(<Min>, New_Queue, LEQV);
			decreasing |Q|;
		do
			Dequeue(Considered_Entry, Q);
			if (Compare(Considered_Entry, Min)) then
				Min :=: Considered_Entry;
			end;	
			Enqueue(Considered_Entry, New_Queue);
		end;
		New_Queue :=: Q;
	end Remove_Min;

end;
\end{lstlisting}

%-----------------------------------------------------------------------------
	\subsection{Stack\_Template}	%-----------------------------------------------------------------------------

%-----------------------------------------------------------------------------
		\subsubsection{Array\_Realiz}	%-----------------------------------------------------------------------------

\begin{lstlisting}[language=resolve]
Realization Array_Realiz for Stack_Template;
	uses Binary_Iterator_Theory;

    Type Stack is represented by Record
            Contents: Array 1..Max_Depth of Entry;
            Top: Integer;
        end;
        convention
            0 <= S.Top <= Max_Depth;
        correspondence
            Conc.S = Reverse(Concatenate(S.Contents, 
		S.Top));

    Procedure Push(alters E: Entry; updates S: Stack);
        S.Top := S.Top + 1;
        E :=: S.Contents[S.Top];
    end;

    Procedure Pop(replaces R: Entry; updates S: Stack); 
        R :=: S.Contents[S.Top];
        S.Top := S.Top - 1;
    end;

    Procedure Depth(preserves S: Stack): Integer;
        Depth := S.Top;
    end;

    Procedure Rem_Capacity(preserves S: Stack): Integer;
        Rem_Capacity := Max_Depth - S.Top;
    end;

    Procedure Clear(clears S: Stack);
        S.Top := 0;
    end;

end;
\end{lstlisting}

%-----------------------------------------------------------------------------
	\subsection{Static\_Array\_Template}	%-----------------------------------------------------------------------------

%-----------------------------------------------------------------------------
		\subsubsection{Bin\_Search\_Realiz}	%-----------------------------------------------------------------------------

\begin{lstlisting}[language=resolve]
Realization Bin_Search_Realiz(
		Operation Are_Ordered(restores x,y: Entry): Boolean;
			ensures Are_Ordered = (LEQ(x,y));)
	for Search_Capability of Static_Array_Template;
	uses Std_Boolean_Fac;

	Operation Are_Equal(restores x, y : Entry) : Boolean;
		ensures Are_Equal = (x = y);
	Procedure 
		Are_Equal := And(Are_Ordered(x, y), Are_Ordered(y, x));
	end; 

	Procedure Is_Present(restores key: Entry; restores A: Static_Array): Boolean;
		Var low, mid, high, one, two : Integer;
		Var midVal : Entry;
		Var result : Boolean;

		one := One();
		two := Two();

		result := False();
		low := Lower_Bound;
		high := Upper_Bound;

		While (low <= high)
			changing low, mid, high, A, midVal, result;
			maintaining result =
				(Exists_Between(key, Concatenate(
						Shift(A, 
							(Lower_Bound - 1) * -1), 
						Upper_Bound - Lower_Bound), 
					Lower_Bound, low - 1) or
				 Exists_Between(key, Concatenate(
						Shift(A, 
							(Lower_Bound - 1) * -1), 
						Upper_Bound - Lower_Bound), 
					high + 1, Upper_Bound))
				and Lower_Bound <= low and high <= Upper_Bound
				and A = #A;
			decreasing (high - low);
		do
			mid := Div(Difference(high, low), two);
			mid := Sum(low, mid);
			Swap_Entry(A, midVal, mid);
			if (Are_Equal(midVal, key)) then
				result := True();
				low := Sum(high, one);
			else 
				if (Are_Ordered(midVal, key)) then
					low := Sum(mid, one);
				else
					high := Difference(mid, one);
				end;
			end;
			Swap_Entry(A, midVal, mid);
		end;

		Is_Present := result;
	end;
end;
\end{lstlisting}
